buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://jitpack.io" }
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }

    dependencies {
        classpath 'com.github.asbyth:forgegradle:8708bf3e01'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.github.jengelman.gradle.plugins:shadow:6.1.0"
    }
}

apply plugin: "kotlin"
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.github.johnrengelman.shadow'

version = project.version
group = project.group
archivesBaseName = project.archivesBaseName

sourceCompatibility = targetCompatibility = 1.8

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
    clientRunArgs += "--tweakClass gg.essential.loader.stage0.EssentialSetupTweaker" // needed to run Essential in dev env
    clientJvmArgs += "-Dfml.coreMods.load=$fmlPluginClass" // needed to use the fml plugin in dev env

    replaceIn "TransitionsMod.java"
    replace "@MOD_VERSION@", version
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url 'https://repo.spongepowered.org/repository/maven-public' }
    maven { url "https://repo.sk1er.club/repository/maven-public" }
}

configurations {
    shadow
    implementation.extendsFrom shadow
}

dependencies {
    shadow "gg.essential:loader-launchwrapper:1.0.2"
    provided("gg.essential:Essential:$essentialVersion") {
        exclude module: 'asm-commons'
        exclude module: 'asm-tree'
        exclude module: 'asm'
        exclude module: 'WaifUPnP'
    }
}

shadowJar {
    archiveBaseName.set(jar.archiveBaseName)
    archiveClassifier.set(jar.archiveClassifier)
    configurations = [project.configurations.shadow]
}

reobfJar {
    setJar(shadowJar.getArchiveFile())
}
tasks.reobfJar.dependsOn shadowJar
jar.enabled = false

jar {
    exclude "club/sk1er/patcher/**" // excludes the dummy Patcher config file, that is used to solve a conflict
    manifest {
        attributes(
                "FMLCorePlugin": fmlPluginClass,
                "TweakClass": "gg.essential.loader.stage0.EssentialSetupTweaker",
                "TweakOrder": "0",
                "ModSide": "CLIENT",
                'FMLAT': accessTransformer
        )
    }
}

task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/java"
    }
}

moveResources.dependsOn processResources
classes.dependsOn moveResources

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
    rename '(.+_at.cfg)', 'META-INF/$1'
}