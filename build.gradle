buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.github.johnrengelman.shadow'

version = "0.1"
group = "io.github.tivj.f5transitions"
archivesBaseName = "F5 Transitions"

sourceCompatibility = targetCompatibility = 1.8

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
    clientRunArgs += " --tweakClass io.github.tivj.f5transitions.asm.tweaker.F5TransitionsTweaker"

    replaceIn "TransitionsMod.java"
    replace "@MOD_VERSION@", project.version
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url 'https://repo.spongepowered.org/maven' }
    maven { url "https://repo.sk1er.club/repository/maven-public" }
}

dependencies {
    compile "net.modcore:Loader:2.0.0"
    provided("net.modcore:ModCore:714-10809-SNAPSHOT") {
        exclude module: 'asm-commons'
        exclude module: 'asm-tree'
        exclude module: 'asm'
    }
}

shadowJar {
    baseName = jar.baseName
    classifier = jar.classifier
}

reobf {
    shadowJar {} // Reobfuscate the shadowed JAR
}

task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/java"
    }
}

moveResources.dependsOn processResources
classes.dependsOn moveResources

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
    rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
    exclude "club/sk1er/patcher/**" // excludes the dummy Patcher config file, that is used to solve a conflict
    manifest {
        attributes(
                "TweakClass": "io.github.tivj.f5transitions.asm.tweaker.F5TransitionsTweaker",
                "TweakOrder": "0",
                "ModSide": "CLIENT",
                'FMLAT': 'f5transitions_at.cfg'
        )
    }
}